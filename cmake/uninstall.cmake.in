set(INSTALL_MANIFEST "@CMAKE_BINARY_DIR@/install_manifest.txt")
if(NOT EXISTS ${INSTALL_MANIFEST})
	message(FATAL_ERROR "Cannot find install manifest: ${INSTALL_MANIFEST}")
endif()

string(ASCII 27 esc)
message("${esc}[36mUninstall the project...${esc}[0m")

file(READ ${INSTALL_MANIFEST} files)
string(REGEX REPLACE "\n" ";" files "${files}")
list(REVERSE files)

foreach(file ${files})
	message(STATUS "Uninstalling: $ENV{DESTDIR}${file}")
	if(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")
		execute_process(COMMAND "@CMAKE_COMMAND@"
			-E rm "$ENV{DESTDIR}${file}"
			OUTPUT_VARIABLE rm_out
			RESULT_VARIABLE rm_retval)
		if(NOT "${rm_retval}" STREQUAL 0)
			message(FATAL_ERROR "Problem when removing: $ENV{DESTDIR}${file}")
		endif()
	else()
		message(STATUS "File: $ENV{DESTDIR}${file} does not exist.")
	endif()
endforeach()
